{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "25a38ef2",
   "metadata": {},
   "source": [
    "# EV Sweep (Severity) and Two EV Comparisons\n",
    "\n",
    "In this notebook I do two things:\n",
    "\n",
    "Part A: Sweep EV size at a fixed location (usually the weakest bus)\n",
    "- EV sizes: 3.6, 7, 11, 22, 30 kW (can be changed)\n",
    "- Record minimum voltage and violation counts\n",
    "\n",
    "Part B: Compare two EV placements (same EV size)\n",
    "- Case 1: both EVs at weakest bus (clustered)\n",
    "- Case 2: one EV at weakest bus and one at source bus (distributed)\n",
    "\n",
    "Outputs:\n",
    "- Topology plot for fixed location\n",
    "- Sweep plots: min V vs size, violations vs size\n",
    "- Topology plots for two EV cases\n",
    "- Voltage profiles for two EV cases\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "eb2e24e9",
   "metadata": {},
   "outputs": [
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "\u001b[32m\u001b[1m  Activating\u001b[22m\u001b[39m project at `/mnt/c/Users/auc009/OneDrive - CSIRO/Documents/power-models-distribution/pmd_ev_experiments`\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "\"../results/figures\""
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using Pkg\n",
    "Pkg.activate(joinpath(@__DIR__, \"..\"))\n",
    "Pkg.instantiate()\n",
    "\n",
    "using PowerModelsDistribution\n",
    "using DataFrames\n",
    "using Ipopt\n",
    "using JuMP\n",
    "using LinearAlgebra\n",
    "using Plots\n",
    "using Graphs\n",
    "using CairoMakie\n",
    "using Statistics\n",
    "\n",
    "const PMD = PowerModelsDistribution\n",
    "\n",
    "file = \"../data/networks/lv_small_feeder/Master.dss\"\n",
    "fig_dir = \"../results/figures\"\n",
    "mkpath(fig_dir)\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b983493b",
   "metadata": {},
   "source": [
    "## Reuse helper functions\n",
    "\n",
    "Copy the helper function block from 02_single_ev.ipynb into this notebook.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "02707113",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "find_math_bus_id_by_name (generic function with 1 method)"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function build_lines_df(eng)\n",
    "    line_dict = get(eng, \"line\", Dict())\n",
    "    rows = NamedTuple[]\n",
    "    for (id, ln) in line_dict\n",
    "        bus1 = get(ln, \"bus1\", get(ln, \"f_bus\", nothing))\n",
    "        bus2 = get(ln, \"bus2\", get(ln, \"t_bus\", nothing))\n",
    "        if bus1 === nothing || bus2 === nothing\n",
    "            continue\n",
    "        end\n",
    "        b1 = split(String(bus1), \".\")[1]\n",
    "        b2 = split(String(bus2), \".\")[1]\n",
    "        L = float(get(ln, \"length\", 1.0))\n",
    "        push!(rows, (Bus1=b1, Bus2=b2, Length=L))\n",
    "    end\n",
    "    return DataFrame(rows)\n",
    "end\n",
    "\n",
    "function build_graph_and_weights(lines_df::DataFrame)\n",
    "    buses = unique(vcat(lines_df.Bus1, lines_df.Bus2))\n",
    "    sort!(buses)\n",
    "    b2i = Dict(b => i for (i,b) in enumerate(buses))\n",
    "    i2b = Dict(i => b for (b,i) in b2i)\n",
    "\n",
    "    g = Graph(length(buses))\n",
    "    w = Dict{Tuple{Int,Int}, Float64}()\n",
    "\n",
    "    for r in eachrow(lines_df)\n",
    "        i = b2i[r.Bus1]\n",
    "        j = b2i[r.Bus2]\n",
    "        add_edge!(g, i, j)\n",
    "        w[(i,j)] = r.Length\n",
    "        w[(j,i)] = r.Length\n",
    "    end\n",
    "    return g, w, b2i, i2b\n",
    "end\n",
    "\n",
    "function dijkstra_distances(g::Graph, w::Dict{Tuple{Int,Int},Float64}, s::Int)\n",
    "    n = nv(g)\n",
    "    dist = fill(Inf, n)\n",
    "    prev = fill(0, n)\n",
    "    visited = falses(n)\n",
    "\n",
    "    dist[s] = 0.0\n",
    "    pq = [(0.0, s)]\n",
    "\n",
    "    while !isempty(pq)\n",
    "        sort!(pq, by=x->x[1])\n",
    "        (d,u) = popfirst!(pq)\n",
    "        if visited[u]\n",
    "            continue\n",
    "        end\n",
    "        visited[u] = true\n",
    "\n",
    "        for v in neighbors(g, u)\n",
    "            nd = d + get(w, (u,v), 1.0)\n",
    "            if nd < dist[v]\n",
    "                dist[v] = nd\n",
    "                prev[v] = u\n",
    "                push!(pq, (nd, v))\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    return dist, prev\n",
    "end\n",
    "\n",
    "function tree_children(prev::Vector{Int})\n",
    "    n = length(prev)\n",
    "    children = [Int[] for _ in 1:n]\n",
    "    for v in 1:n\n",
    "        p = prev[v]\n",
    "        if p != 0\n",
    "            push!(children[p], v)\n",
    "        end\n",
    "    end\n",
    "    return children\n",
    "end\n",
    "\n",
    "function tree_layout(prev::Vector{Int}, dist_km::Vector{Float64}, root::Int)\n",
    "    children = tree_children(prev)\n",
    "    y = fill(0.0, length(prev))\n",
    "    next_y = Ref(0.0)\n",
    "\n",
    "    function dfs(u::Int)\n",
    "        if isempty(children[u])\n",
    "            y[u] = next_y[]\n",
    "            next_y[] += 1.0\n",
    "        else\n",
    "            for c in sort(children[u])\n",
    "                dfs(c)\n",
    "            end\n",
    "            y[u] = mean(y[children[u]])\n",
    "        end\n",
    "    end\n",
    "\n",
    "    dfs(root)\n",
    "    x = copy(dist_km)\n",
    "    return x, y\n",
    "end\n",
    "\n",
    "function plot_topology_with_markers(\n",
    "    g::Graph, xpos::Vector{Float64}, ypos::Vector{Float64}, dist_km::Vector{Float64};\n",
    "    source_idx::Int,\n",
    "    ev_indices::Vector{Int}=Int[],\n",
    "    title_str::String=\"Topology (colored by distance)\",\n",
    "    save_path::Union{Nothing,String}=nothing\n",
    ")\n",
    "    fig = Figure(size=(1000, 700))\n",
    "    ax = Axis(fig[1,1], title=title_str)\n",
    "\n",
    "    for e in edges(g)\n",
    "        u = src(e); v = dst(e)\n",
    "        lines!(ax, [xpos[u], xpos[v]], [ypos[u], ypos[v]], color=:gray50, linewidth=2)\n",
    "    end\n",
    "\n",
    "    scatter!(ax, xpos, ypos, color=dist_km, markersize=10)\n",
    "    scatter!(ax, [xpos[source_idx]], [ypos[source_idx]], markersize=22, color=:black)\n",
    "    text!(ax, \"source\", position=(xpos[source_idx], ypos[source_idx] + 0.6), align=(:center, :bottom))\n",
    "\n",
    "    for (k, ev) in enumerate(ev_indices)\n",
    "        scatter!(ax, [xpos[ev]], [ypos[ev]], markersize=22, color=:red)\n",
    "        text!(ax, \"EV$(k)\", position=(xpos[ev], ypos[ev] + 0.6), align=(:center, :bottom))\n",
    "    end\n",
    "\n",
    "    hidedecorations!(ax)\n",
    "    hidespines!(ax)\n",
    "\n",
    "    if save_path !== nothing\n",
    "        save(save_path, fig)\n",
    "    end\n",
    "    return fig\n",
    "end\n",
    "\n",
    "function busid_to_name_map(math)\n",
    "    m = Dict{String,String}()\n",
    "    for (id, b) in math[\"bus\"]\n",
    "        m[string(id)] = split(get(b, \"name\", string(id)), \".\")[1]\n",
    "    end\n",
    "    return m\n",
    "end\n",
    "\n",
    "function extract_vm_by_busid(result)\n",
    "    vm = Dict{String, Vector{Float64}}()\n",
    "    for (id, b) in result[\"solution\"][\"bus\"]\n",
    "        vr = b[\"vr\"]; vi = b[\"vi\"]\n",
    "        vm[string(id)] = sqrt.(vr.^2 .+ vi.^2)\n",
    "    end\n",
    "    return vm\n",
    "end\n",
    "\n",
    "function build_buses_dict_for_voltage_plot(id2name::Dict{String,String}, dist_km_byname::Dict{String,Float64}, vm_byid::Dict{String,Vector{Float64}})\n",
    "    buses_dict = Dict{String, Dict{String,Any}}()\n",
    "    for (id, vm) in vm_byid\n",
    "        name = id2name[id]\n",
    "        vma = length(vm) >= 1 ? vm[1] : NaN\n",
    "        vmb = length(vm) >= 2 ? vm[2] : NaN\n",
    "        vmc = length(vm) >= 3 ? vm[3] : NaN\n",
    "        buses_dict[name] = Dict(\n",
    "            \"distance\" => get(dist_km_byname, name, NaN),\n",
    "            \"vma\" => [float(vma)],\n",
    "            \"vmb\" => [float(vmb)],\n",
    "            \"vmc\" => [float(vmc)]\n",
    "        )\n",
    "    end\n",
    "    return buses_dict\n",
    "end\n",
    "\n",
    "function plot_voltage_along_feeder_snap(buses_dict, lines_df; t=1, vmin=0.9, vmax=1.1)\n",
    "    p = plot(legend=false)\n",
    "    xlabel!(\"Electrical distance from source (km)\")\n",
    "    ylabel!(\"Voltage magnitude (p.u.)\")\n",
    "    title!(\"Voltage drop along feeder (snapshot)\")\n",
    "\n",
    "    colors = [:blue, :red, :black]\n",
    "\n",
    "    for r in eachrow(lines_df)\n",
    "        b1 = r.Bus1\n",
    "        b2 = r.Bus2\n",
    "        for phase in 1:3\n",
    "            d1 = buses_dict[b1][\"distance\"]\n",
    "            d2 = buses_dict[b2][\"distance\"]\n",
    "            v1 = phase==1 ? buses_dict[b1][\"vma\"][t] : phase==2 ? buses_dict[b1][\"vmb\"][t] : buses_dict[b1][\"vmc\"][t]\n",
    "            v2 = phase==1 ? buses_dict[b2][\"vma\"][t] : phase==2 ? buses_dict[b2][\"vmb\"][t] : buses_dict[b2][\"vmc\"][t]\n",
    "            if isfinite(d1) && isfinite(d2) && isfinite(v1) && isfinite(v2)\n",
    "                plot!([d1, d2], [v1, v2], color=colors[phase], marker=:circle, markersize=1)\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    maxdist = maximum([b[\"distance\"] for (k,b) in buses_dict if isfinite(b[\"distance\"])])\n",
    "    plot!([0, maxdist], [vmin, vmin], color=:red, linestyle=:dash)\n",
    "    plot!([0, maxdist], [vmax, vmax], color=:red, linestyle=:dash)\n",
    "    return p\n",
    "end\n",
    "\n",
    "function add_ev_load!(math, bus_id::String; phase::Int=1, P_kw::Float64=7.0, Q_kvar::Float64=0.0)\n",
    "    sbase = get(math[\"settings\"], \"sbase\", 1.0)\n",
    "    P_pu = P_kw / (sbase * 1000)\n",
    "    Q_pu = Q_kvar / (sbase * 1000)\n",
    "\n",
    "    existing = parse.(Int, collect(keys(math[\"load\"])))\n",
    "    new_id = string(isempty(existing) ? 1 : maximum(existing) + 1)\n",
    "\n",
    "    math[\"load\"][new_id] = Dict(\n",
    "        \"bus\" => bus_id,\n",
    "        \"connections\" => [phase],\n",
    "        \"pd\" => [P_pu],\n",
    "        \"qd\" => [Q_pu],\n",
    "        \"status\" => 1,\n",
    "        \"model\" => \"constant_power\"\n",
    "    )\n",
    "    return new_id\n",
    "end\n",
    "\n",
    "function find_math_bus_id_by_name(id2name::Dict{String,String}, target_name::String)\n",
    "    for (id, nm) in id2name\n",
    "        if nm == target_name\n",
    "            return id\n",
    "        end\n",
    "    end\n",
    "    error(\"No math bus id found for name '$target_name'\")\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "dcf9a7f7",
   "metadata": {},
   "outputs": [],
   "source": [
    "eng4w = PMD.parse_file(file, transformations=[PMD.transform_loops!])\n",
    "eng4w[\"settings\"][\"sbase_default\"] = 1.0 * 1e3 / eng4w[\"settings\"][\"power_scale_factor\"]\n",
    "PMD.add_bus_absolute_vbounds!(eng4w, phase_lb_pu=0.9, phase_ub_pu=1.1, neutral_ub_pu=0.1)\n",
    "\n",
    "lines_df = build_lines_df(eng4w)\n",
    "g, w, b2i, i2b = build_graph_and_weights(lines_df)\n",
    "\n",
    "source_name = \"sourcebus\"\n",
    "source_idx = b2i[source_name]\n",
    "dist_raw, prev = dijkstra_distances(g, w, source_idx)\n",
    "\n",
    "scale_to_km = 1.0 / 1000\n",
    "dist_km = dist_raw .* scale_to_km\n",
    "xpos, ypos = tree_layout(prev, dist_km, source_idx)\n",
    "dist_km_byname = Dict(i2b[i] => dist_km[i] for i in 1:length(dist_km) if isfinite(dist_km[i]))\n",
    "\n",
    "math_base = PMD.transform_data_model(eng4w; multinetwork=false, kron_reduce=false, phase_project=false)\n",
    "id2name = busid_to_name_map(math_base)\n",
    "\n",
    "res_base = PMD.solve_mc_opf(math_base, IVRENPowerModel, Ipopt.Optimizer)\n",
    "vm_base = extract_vm_by_busid(res_base)\n",
    "buses_dict_base = build_buses_dict_for_voltage_plot(id2name, dist_km_byname, vm_base)\n",
    "\n",
    "p_base = plot_voltage_along_feeder_snap(buses_dict_base, lines_df; vmin=0.9, vmax=1.1)\n",
    "savefig(p_base, joinpath(fig_dir, \"04_voltage_profile_baseline.png\"))\n",
    "\n",
    "source_bus_id = string(math_base[\"bus_lookup\"][\"sourcebus\"])\n",
    "source_bus_name = id2name[source_bus_id]\n",
    "\n",
    "# Weakest bus\n",
    "minV_base_byid = Dict(id => minimum(vm[1:min(3, length(vm))]) for (id, vm) in vm_base)\n",
    "weakest_bus_id = first(keys(minV_base_byid))\n",
    "weakest_val = minV_base_byid[weakest_bus_id]\n",
    "for (id, v) in minV_base_byid\n",
    "    if v < weakest_val\n",
    "        weakest_val = v\n",
    "        weakest_bus_id = id\n",
    "    end\n",
    "end\n",
    "weakest_name = id2name[weakest_bus_id]\n",
    "\n",
    "println(\"Weakest bus: \", weakest_name, \" minV=\", weakest_val)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b355c639",
   "metadata": {},
   "source": [
    "## Part A: EV severity sweep at fixed location\n",
    "\n",
    "I fix the EV at the weakest bus and increase EV power.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "5866dcab",
   "metadata": {},
   "outputs": [],
   "source": [
    "fixed_bus_name = weakest_name\n",
    "fixed_bus_id = weakest_bus_id\n",
    "fixed_bus_idx = b2i[fixed_bus_name]\n",
    "\n",
    "plot_topology_with_markers(\n",
    "    g, xpos, ypos, dist_km;\n",
    "    source_idx=source_idx,\n",
    "    ev_indices=[fixed_bus_idx],\n",
    "    title_str=\"Topology with fixed EV location (sweep at weakest bus)\",\n",
    "    save_path=joinpath(fig_dir, \"04_topology_sweep_fixed_weakest.png\")\n",
    ")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "264d0584",
   "metadata": {},
   "outputs": [],
   "source": [
    "P_levels_kw = [3.6, 7.0, 11.0, 22.0, 30.0]\n",
    "ev_phase = 1\n",
    "\n",
    "rows = NamedTuple[]\n",
    "\n",
    "for PkW in P_levels_kw\n",
    "    math = deepcopy(math_base)\n",
    "    add_ev_load!(math, fixed_bus_id; phase=ev_phase, P_kw=PkW, Q_kvar=0.0)\n",
    "\n",
    "    res = PMD.solve_mc_opf(math, IVRENPowerModel, Ipopt.Optimizer)\n",
    "    vm = extract_vm_by_busid(res)\n",
    "\n",
    "    push!(rows, (\n",
    "        P_kw=PkW,\n",
    "        status=string(res[\"termination_status\"]),\n",
    "        minV=min_phase_voltage(vm),\n",
    "        violations=count_voltage_violations(vm; vmin=0.9, vmax=1.1)\n",
    "    ))\n",
    "end\n",
    "\n",
    "sweep_df = DataFrame(rows)\n",
    "sweep_df"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b5bc806c",
   "metadata": {},
   "outputs": [],
   "source": [
    "p1 = plot(sweep_df.P_kw, sweep_df.minV,\n",
    "    xlabel=\"EV power (kW)\",\n",
    "    ylabel=\"Minimum phase voltage (p.u.)\",\n",
    "    title=\"EV severity sweep at weakest bus\",\n",
    "    legend=false,\n",
    "    marker=:circle\n",
    ")\n",
    "hline!([0.9], linestyle=:dash)\n",
    "savefig(p1, joinpath(fig_dir, \"04_sweep_minV_vs_P.png\"))\n",
    "\n",
    "p2 = plot(sweep_df.P_kw, sweep_df.violations,\n",
    "    xlabel=\"EV power (kW)\",\n",
    "    ylabel=\"Number of buses with voltage violation\",\n",
    "    title=\"Voltage violations vs EV power\",\n",
    "    legend=false,\n",
    "    marker=:circle\n",
    ")\n",
    "savefig(p2, joinpath(fig_dir, \"04_sweep_violations_vs_P.png\"))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ecf01fd5",
   "metadata": {},
   "source": [
    "## Part B: Two EV comparisons\n",
    "\n",
    "I compare clustered vs distributed EV placement.\n",
    "\n",
    "Case A: two EVs at weakest bus\n",
    "Case B: one EV at weakest bus and one EV at source bus\n",
    "\n",
    "I use the same EV size for each EV (for example 7 kW).\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "27f8d227",
   "metadata": {},
   "outputs": [],
   "source": [
    "P_two_kw = 7.0\n",
    "ev_phase = 1\n",
    "\n",
    "# Case A: clustered\n",
    "caseA = deepcopy(math_base)\n",
    "add_ev_load!(caseA, weakest_bus_id; phase=ev_phase, P_kw=P_two_kw, Q_kvar=0.0)\n",
    "add_ev_load!(caseA, weakest_bus_id; phase=ev_phase, P_kw=P_two_kw, Q_kvar=0.0)\n",
    "\n",
    "resA = PMD.solve_mc_opf(caseA, IVRENPowerModel, Ipopt.Optimizer)\n",
    "vmA = extract_vm_by_busid(resA)\n",
    "busesA = build_buses_dict_for_voltage_plot(id2name, dist_km_byname, vmA)\n",
    "\n",
    "# Case B: distributed\n",
    "caseB = deepcopy(math_base)\n",
    "add_ev_load!(caseB, weakest_bus_id; phase=ev_phase, P_kw=P_two_kw, Q_kvar=0.0)\n",
    "add_ev_load!(caseB, source_bus_id;  phase=ev_phase, P_kw=P_two_kw, Q_kvar=0.0)\n",
    "\n",
    "resB = PMD.solve_mc_opf(caseB, IVRENPowerModel, Ipopt.Optimizer)\n",
    "vmB = extract_vm_by_busid(resB)\n",
    "busesB = build_buses_dict_for_voltage_plot(id2name, dist_km_byname, vmB)\n",
    "\n",
    "# Topology plots\n",
    "weak_idx = b2i[weakest_name]\n",
    "src_idx2 = b2i[source_bus_name]\n",
    "\n",
    "plot_topology_with_markers(\n",
    "    g, xpos, ypos, dist_km;\n",
    "    source_idx=source_idx,\n",
    "    ev_indices=[weak_idx, weak_idx],\n",
    "    title_str=\"Two EVs clustered at weakest bus\",\n",
    "    save_path=joinpath(fig_dir, \"04_topology_twoEV_clustered.png\")\n",
    ")\n",
    "\n",
    "plot_topology_with_markers(\n",
    "    g, xpos, ypos, dist_km;\n",
    "    source_idx=source_idx,\n",
    "    ev_indices=[weak_idx, src_idx2],\n",
    "    title_str=\"Two EVs distributed (weakest + source)\",\n",
    "    save_path=joinpath(fig_dir, \"04_topology_twoEV_distributed.png\")\n",
    ")\n",
    "\n",
    "# Voltage profile plots\n",
    "pA = plot_voltage_along_feeder_snap(busesA, lines_df; vmin=0.9, vmax=1.1)\n",
    "plot!(pA, p_base)\n",
    "savefig(pA, joinpath(fig_dir, \"04_voltage_profile_twoEV_clustered.png\"))\n",
    "\n",
    "pB = plot_voltage_along_feeder_snap(busesB, lines_df; vmin=0.9, vmax=1.1)\n",
    "plot!(pB, p_base)\n",
    "savefig(pB, joinpath(fig_dir, \"04_voltage_profile_twoEV_distributed.png\"))\n",
    "\n",
    "println(\"Case A minV: \", min_phase_voltage(vmA), \" violations: \", count_voltage_violations(vmA))\n",
    "println(\"Case B minV: \", min_phase_voltage(vmB), \" violations: \", count_voltage_violations(vmB))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f6cb7847",
   "metadata": {},
   "source": [
    "## Notes\n",
    "\n",
    "This notebook tells me:\n",
    "- how voltage worsens as EV size increases at a stressed location\n",
    "- whether clustering EVs is worse than spreading them out\n",
    "\n",
    "Next step after this is to pick a stressed case where voltage limits are violated.\n",
    "That case becomes the input scenario for STATCOM experiments.\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d49b7a5e",
   "metadata": {},
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
